# SPDX-License-Identifier: GPL-3.0-or-later
# SPDX-FileCopyrightText: Andy Holmes <andrew.g.r.holmes@gmail.com>

# Inputs: iri
# Outputs: message, box, date, messageId, read, recipients, sender, subscriptionId, text, threadId
#          communicationChannel, participants
SELECT
  ?message
  ?box
  ?date
  ?messageId
  ?read
  (GROUP_CONCAT(?recipient; separator=",") AS ?recipients)
  ?sender
  ?subscriptionId
  ?text
  ?threadId
  ?communicationChannel
  (GROUP_CONCAT(?participant; separator=",") AS ?participants)
WHERE {
  {
    SELECT ?communicationChannel (MAX(?date) AS ?latestDate)
    WHERE {
      BIND(IRI(xsd:string(~iri)) AS ?communicationChannel)
      ?message vmo:communicationChannel ?communicationChannel ;
               dc:date ?date .
    }
    GROUP BY ?communicationChannel
  }
  FILTER(?date = ?latestDate)
  ?message rdf:type vmo:PhoneMessage ;
           vmo:phoneMessageBox/vmo:phoneMessageBoxId ?box ;
           dc:date ?date ;
           vmo:phoneMessageId ?messageId ;
           nmo:isRead ?read ;
           vmo:subscriptionId ?subscriptionId ;
           vmo:communicationChannel ?communicationChannel .
  ?communicationChannel rdf:type vmo:CommunicationChannel ;
                        vmo:communicationChannelId ?threadId ;
                        vmo:hasParticipant ?participant .
  OPTIONAL {
    ?message nmo:primaryMessageRecipient/(nco:phoneNumber|nco:emailAddress) ?recipient
  }
  OPTIONAL {
    ?message nmo:messageSender/(nco:phoneNumber|nco:emailAddress) ?sender
  }
  OPTIONAL {
    ?message nmo:plainTextMessageContent ?text
  }
}

