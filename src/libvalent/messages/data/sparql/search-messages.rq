# SPDX-License-Identifier: GPL-3.0-or-later
# SPDX-FileCopyrightText: Andy Holmes <andrew.g.r.holmes@gmail.com>

# Inputs: query
# Outputs: message, box, date, messageId, read, recipients, sender, subscriptionId, text, threadId
#          attachment, encoded_thumbnail, fileUri
SELECT
  ?message
  ?box
  ?date
  ?messageId
  ?read
  (GROUP_CONCAT(?recipient; separator=",") AS ?recipients)
  ?sender
  ?subscriptionId
  ?text
  ?threadId
  ?attachment
  ?encoded_thumbnail
  ?fileUri
WHERE {
  {
    SELECT ?threadId (MAX(?date) AS ?latestDate)
    WHERE {
      ?message rdf:type vmo:PhoneMessage ;
               dc:date ?date ;
               nmo:plainTextMessageContent ?text ;
               vmo:communicationChannel/vmo:communicationChannelId ?threadId ;
               fts:match ~query^^xsd:string .
    }
    GROUP BY ?threadId
  }
  ?message rdf:type vmo:PhoneMessage ;
           vmo:phoneMessageBox/vmo:phoneMessageBoxId ?box ;
           dc:date ?date ;
           vmo:phoneMessageId ?messageId ;
           nmo:isRead ?read ;
           vmo:subscriptionId ?subscriptionId ;
           vmo:communicationChannel/vmo:communicationChannelId ?threadId .
  FILTER(?date = ?latestDate)
  OPTIONAL {
    ?message nmo:hasAttachment ?attachment .
    OPTIONAL { ?attachment rdf:type nfo:Attachment }
    OPTIONAL { ?attachment vmo:encoded_thumbnail ?encoded_thumbnail }
    OPTIONAL { ?attachment nie:url ?fileUri }
  }
  OPTIONAL {
    ?message nmo:primaryMessageRecipient/(nco:phoneNumber|nco:emailAddress) ?recipient
  }
  OPTIONAL {
    ?message nmo:messageSender/(nco:phoneNumber|nco:emailAddress) ?sender
  }
  OPTIONAL {
    ?message nmo:plainTextMessageContent ?text
  }
}
GROUP BY ?message ?attachment
ORDER BY DESC(fts:rank(?message))
LIMIT 5

